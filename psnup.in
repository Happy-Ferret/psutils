#!/usr/bin/env perl
# @configure_input@
my $version_banner = <<END;
FIXME: generate this banner
psnup 2.00
Copyright (c) Reuben Thomas 2016
Released under the GPL version 3, or (at your option) any later version.
END

use v5.14;
use warnings;
use strict;
no if $] >= 5.018, warnings => "experimental::smartmatch";

BEGIN
{
  @PSUtils::perl_libdirs = ('@pkgdatadir@') unless @PSUtils::perl_libdirs;
  unshift @INC, @PSUtils::perl_libdirs;
}

use File::Basename;
use Getopt::Long;
use List::Util qw(min);

use PSUtils;

my $prog = basename($0);
my ($help_flag, $version_flag);

sub usage {
  my ($exit_code, $message) = @_;
  say STDERR $message if $message;
  print STDERR <<END;
$prog [OPTION...] -NUP [INFILE [OUTFILE]]

Put multiple pages of a PostScript file on to one page

  --paper=PAPER        output paper name or dimensions
  --inpaper=PAPER      input paper name or dimensions
  --margin=DIMENSION   width of margin around each output page
  --border=DIMENSION   width of border around each input page
  --draw[=DIMENSION]   draw a line of given width around each page
                       [argument defaults to 1; default is 0]
  --landscape          pages are landscape (rotated left)
  --seascape           pages are seascape (rotated right)
  --flipped            pages have width and height swapped
  --columnmajor        use column major layout
  --tolerance=NUMBER   maximum page wastage in square points [100,000]
  --quiet              don't show page numbers being output
  --help               display this help and exit
  --version            display version information and exit
END
  exit $exit_code;
}

# Settings
my ($horiz, $vert) = (0, 0);
my $rotate = 0;
my $column = 0;
my $flip = 0;
my ($leftright, $topbottom) = (1, 1);
my $nup = 1;
my $draw = 0;				# draw page borders
my $scale = 1.0;			# page scale
my ($margin, $border) = (0, 0);		# paper & page margins
my ($vshift, $hshift) = (0, 0);		# page centring shifts
my ($width, $height);			# output paper size
my ($iwidth, $iheight);			# input paper size
my $tolerance = 100000;			# layout tolerance

# Parse -NUP, which Getopt::Long can't easily be made to understand
sub parsenup {
  die("Option $_[0] requires an argument") unless $_[1] ne "";
  die("Value \"$nup\" invalid for -NUP (number expected)") unless $_[1] =~ /^\d+$/;
  die("number of pages per sheet must be positive") if $_[1] == 0;
  $nup = $_[1];
}

for (my $i = 0; $i <= $#ARGV; ) {
  if ($ARGV[$i] =~ /^-\d+/) { # looks like an option
    die("Value \"$ARGV[$i]\" invalid for -NUP (number expected)") unless $ARGV[$i] =~ /^-\d+$/;
    $nup = -$ARGV[$i];
    die("number of pages per sheet must be positive") if $nup == 0;
    splice(@ARGV, $i, 1)
  } else {
    $i++;
  }
}

# Get arguments
Getopt::Long::Configure("bundling");
# Having configured bundling, must give short options explicitly
my @pstops_args = ();
GetOptions(
  "paper|p=s" => sub { push @pstops_args, "-p", $_[1];
                       ($width, $height) = parsepaper(@_); },
  "width|w=s" => sub { $width = singledimen($_[1], $width, $height); },
  "height|h=s" => sub { $height = singledimen($_[1], $width, $height); },
  "inpaper|P=s" => sub { ($iwidth, $iheight) = parsepaper(@_); },
  "inwidth|W=s" => sub { $iwidth = singledimen($_[1], $width, $height); },
  "inheight|H=s" => sub { $iheight = singledimen($_[1], $width, $height); },
  "margin|m=s" => sub { $margin = singledimen($_[1], $width, $height); },
  "border|b=s" => sub { $border = singledimen($_[1], $width, $height); },
  "draw|d:s" => sub { push @pstops_args, "-d";
                      $draw = singledimen($_[1] || "1pt", $width, $height); },
  "landscape|l" => sub { $column = !$column; $topbottom = !$topbottom; },
  "seascape|r" => sub { $column = !$column; $leftright = !$leftright; },
  "flipped|f" => \$flip,
  "columnmajor|c" => sub { $column = !$column; },
  "tolerance|t=i" => \$tolerance,
  "quiet|q" => sub { push @pstops_args, "-q"; },
  "help" => \$help_flag,
  "version" => \$version_flag,
  "n=s" => \&parsenup, # for compatibility with other psnup implementations
 ) or usage(1);
if ($version_flag) {
  print STDERR $version_banner;
  exit 0;
}
usage(0) if $help_flag;

# Return next larger exact divisor of number, or 0 if none. There is probably
# a much more efficient method of doing this, but the numbers involved are
# small.
sub nextdiv {
  my ($n, $m) = @_;
  while (++$n <= $m) {
    return $n if $m % $n == 0;
  }
  return 0;
}

# Set output height/width from corresponding input value if undefined
($width, $height) = ($iwidth, $iheight) if defined($iwidth);

# Ensure output paper size is set
($width, $height) = paper_size()
  or die("output paper size not set, and could not get default paper size")
  if !defined($width);

# Set input height/width from corresponding output value if undefined
($iwidth, $iheight) = ($width, $height) if !defined($iwidth);

# Calculate paper dimensions, subtracting paper margins from height & width
my ($ppwid, $pphgt) = ($width - $margin * 2, $height - $margin * 2);

die("margins is too large") if $ppwid <= 0 || $pphgt <= 0;
die("border is too large") if $border > min($ppwid, $pphgt);

# Finding the best layout is an optimisation problem. We try all of the
# combinations of width*height in both normal and rotated form, and
# minimise the wasted space.
my $best = $tolerance;

sub optim {
  my ($hor, $ver, $iwid, $ihgt, $flip) = @_;
  my $scl = min($pphgt / ($ihgt * $ver), $ppwid / ($iwid * $hor));
  my $optim = ($ppwid - $scl * $iwid * $hor) ** 2 + ($pphgt - $scl * $ihgt * $ver) ** 2;
  if ($optim < $best) {
    $best = $optim;
    # Recalculate scale to allow for internal borders
    $scale = min(($pphgt - 2 * $border * $ver) / ($ihgt * $ver),
                 ($ppwid - 2 * $border * $hor) / ($iwid * $hor));
    $hshift = ($ppwid / $hor - $iwid * $scale) / 2;
    $vshift = ($pphgt / $ver - $ihgt * $scale) / 2;
    ($horiz, $vert) = ($hor, $ver);
    $rotate = $flip;
  }
}

for (my $hor = 1; $hor != 0; $hor = nextdiv($hor, $nup)) {
  my $ver = $nup / $hor;
  optim($hor, $ver, $iwidth, $iheight, $flip); # normal orientation
  optim($ver, $hor, $iheight, $iwidth, !$flip); # rotated orientation
}

# Fail if nothing better than worst tolerance was found
die("can't find acceptable layout for $nup-up") if $best == $tolerance;

($width, $height) = ($height, $width) if $flip;	# swap width & height for clipping

push @pstops_args, "-w", $width, "-h", $height; # set width and height for pstops

if ($rotate) {	# rotate leftright and topbottom orders
  ($topbottom, $leftright) = (!$leftright, $topbottom);
  $column = !$column;
}

# Construct specification list
my $specs = "$nup:";
for (my $page = 0; $page < $nup; ) {
  my ($up, $across); # page index
  if ($column) {
    $across = $leftright ? int($page / $vert) : $horiz - 1 - int($page / $vert);
    $up = $topbottom ? $vert - 1 - $page % $vert : $page % $vert;
  } else {
    $across = $leftright ? $page % $horiz : $horiz - 1 - $page % $horiz;
    $up = $topbottom ? $vert - 1 - int($page / $horiz) : int($page / $horiz);
  }
  $specs .= $page . ($rotate ? "L" : "") . "@" . $scale .
    "(" . ($rotate ? $margin + ($across + 1) * $ppwid / $horiz - $hshift : $margin + $across * $ppwid / $horiz + $hshift) . "," .
    ($margin + $up * $pphgt / $vert + $vshift) . ")";
  $specs .= "+" if ++$page < $nup;
}

# Rearrange pages
exec("pstops", "-S", @pstops_args, $specs, @ARGV) or die("error running pstops");
